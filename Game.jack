class Game{
    field Random random;
    field PopsApples pop;
    constructor Game new(int rand){
        let random = Random.new(rand);
        do Sys.wait(100);
        return this;
    }
    
    //check if the current apple replace on the correct location
    method int isGoodLoc(int location, Array arr, int i){
        var int a;
        var int b;
        let a = location-4096;
        let b = Math.divide(a, 5);
        if(~(b=1)&~(b=2)&~(b=3)&~(b=4)&~(b=5)){
            return 0;
        }
        if(arr[b-1] = i){
            return 1;
        }
        else{
            return 0;
        }
    }

    method int play(){
        var int i;
        var Apple apple;
        var Array numArr;
        var int key;
        var int ok;
        //define bowls
        var Bowl bowl1;
        var Bowl bowl2;
        var Bowl bowl3;
        var Bowl bowl4;
        var Bowl bowl5;
        let bowl1 = Bowl.new(4);
        let bowl2 = Bowl.new(9);
        let bowl3 = Bowl.new(14);
        let bowl4 = Bowl.new(19);
        let bowl5 = Bowl.new(24);

        // init array to be [1, 2, 3, 4, 5], than shuffle it.
        let numArr = Array.new(5);
        let numArr[0] = 1; 
        let numArr[1] = 2;
        let numArr[2] = 3;
        let numArr[3] = 4;
        let numArr[4] = 5;
        let numArr = random.shuffle(numArr);
        
        //start pop apples acording to numArr
        let i = 0;
        while(i<5){
           let pop = PopsApples.new(numArr[i], i+1);
           do pop.popUp();
           do pop.popDown();
           let i=i+1;
        }

        do Sys.wait(400);
        //earase apples
        let i = 0;
        while(i<5){
           let apple = Apple.new(((i+1)*5)+4096, numArr[i]);
           do apple.earaseApple();
           let i=i+1;
        }

        //the user start play
        let ok = 1;
        let i=1;
        while(i<6){
            let apple = Apple.new(1, i);
            let key = 0;
            while(~(key = 128)){
                let key = Keyboard.keyPressed();
                if(key = 130){
                    do apple.moveLeft();
                }
                if(key = 131){
                    do apple.moveUp();
                }
                if(key = 132){
                    do apple.moveRight();
                }
                if(key = 133){
                    do apple.moveDown();
                }
                
                do Sys.wait(100);
            }
            if(ok = 1){
                let ok = isGoodLoc(apple.getLocation(), numArr, i);
            }

            let i = i+1;
        }

        return ok;
    }
 
    method void dispose(){
        do Memory.deAlloc(this);
        return;
    }

}